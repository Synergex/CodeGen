;;*****************************************************************************
;;
;; Title:       RpsKeySegmentCollection.dbl
;;
;; Type:        Class
;;
;; Description: Represents a collection of key segments for a Repository
;;              structures key
;;
;; Date:        19th October 2007
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import CodeGen.RepositoryAPI
import System.Collections

namespace CodeGen.RepositoryAPI

    public class RpsKeySegmentCollection

        private mCollection, @ArrayList

        ;;Constructor
        public method RpsKeySegmentCollection
            required in KeyInfo, k_info
            endparams
        proc
            RpsUtils.DbgLog("RpsKeySegmentCollection(KeyInfo) constructor")
            mCollection = new ArrayList()
            LoadKeySegments(KeyInfo)
            mreturn
        endmethod

        ;; Destructor
        method ~RpsKeySegmentCollection
        proc
            clear mCollection
        endmethod

        ;;Load key segment information
        private method LoadKeySegments, void
            required in KeyInfo, k_info
            endparams
            record
                ix, int
            endrecord
        proc
            for ix from 1 thru KeyInfo.ki_nmseg
            begin
                data seg, @RpsKeySegment
                seg = new RpsKeySegment(
                &   KeyInfo.ki_segtyp[ix],
                &   KeyInfo.ki_segpos[ix],
                &   KeyInfo.ki_seglen[ix],
                &   KeyInfo.ki_fldnam[ix],
                &   KeyInfo.ki_strnam[ix],
                &   KeyInfo.ki_litval[ix],
                &   KeyInfo.ki_segdtyp[ix],
                &   KeyInfo.ki_segord[ix])
                mCollection.Add(seg)
            end

        endmethod

        ;;---------------------------------------------------------------------
        ;; Public methods and properties

        ;;; <summary>
        ;;; Expose the internal ArrayList so consumers can use FOREACH
        ;;; </summary>
        ;;; <returns>Internal ArrayList used to store the collection.</returns>
        public method GetEnumerator, @ArrayList
            endparams
        proc
            mreturn mCollection
        endmethod

        ;;---------------------------------------------------------------------
        ;;The follwoing members make the collection look just like an ArrayList

        ;;Gets or sets the number of elements that the collection can contain.
        public property Capacity, int
            method get
            proc
                mreturn mCollection.Capacity
            endmethod
            method set
            proc
                mCollection.Capacity = value
            endmethod
        endproperty

        ;;Gets the number of elements actually contained in the collection.
        public property Count, int
            method get
            proc
                mreturn mCollection.Count
            endmethod
        endproperty

        ;;Gets or sets the element at the specified index.
        public property Indexer, @RpsKeySegment
            required in Index, int
            method get
            proc
                mreturn (RpsKeySegment)mCollection[Index]
            endmethod
            method set
            proc
                mCollection[Index] = value
            endmethod
        endproperty

        ;;Adds an element to the end of the collection.
        public method Add, int
            required in RpsKeySegment, @RpsKeySegment
            endparams
        proc
            mreturn mCollection.Add(RpsKeySegment)
        endmethod

        ;;Removes the element at the specified index of the collection.
        public method RemoveAt, void
            required in Index, int
        proc
            mCollection.RemoveAt(Index)
        endmethod

        ;;Searches for the specified element and returns the one-based index
        ;;of the first occurrence within the entire collection.
        public method IndexOf, int
            required in RpsKeySegment, @RpsKeySegment
        proc
            mreturn mCollection.IndexOf(RpsKeySegment)
        endmethod

        ;;Searches for the specified element and returns the one-based index
        ;;of the first occurrence within the range of elements in the collection
        ;;that extends from the specified index to the last element.
        public method IndexOf, int
            required in RpsKeySegment, @RpsKeySegment
            required in StartIndex, int
        proc
            mreturn mCollection.IndexOf(RpsKeySegment,StartIndex)
        endmethod

        ;;Searches for the specified element and returns the one-based index
        ;;of the first occurrence within the range of elements in the collection
        ;;that starts at the specified index and contains the specified number of
        ;;elements.
        public method IndexOf, int
            required in RpsKeySegment, @RpsKeySegment
            required in StartIndex, int
            required in Count, int
        proc
            mreturn mCollection.IndexOf(RpsKeySegment,StartIndex,Count)
        endmethod

        ;;Removes all elements from the collection.
        public method Clear, void
        proc
            mCollection.Clear()
        endmethod

        ;;Inserts an element into the collection at the specified index.
        public method Insert, void
            required in AtIndex, int
            required in RpsKeySegment, @RpsKeySegment
            endparams
        proc
            mCollection.Insert(AtIndex,RpsKeySegment)
        endmethod

        ;;Searches for the specified object and returns the one-based index of
        ;;the last occurrence within the entire collection.
        public method LastIndexOf, int
            required in RpsKeySegment, @RpsKeySegment
        proc
            mreturn mCollection.LastIndexOf(RpsKeySegment)
        endmethod

        ;;Searches for the specified object and returns the one-based index of
        ;;the last occurrence within the range of elements in the collection that
        ;;extends from the first element to the specified index.
        public method LastIndexOf, int
            required in RpsKeySegment, @RpsKeySegment
            required in StartIndex, int
        proc
            mreturn mCollection.LastIndexOf(RpsKeySegment,StartIndex)
        endmethod

        ;;Searches for the specified object and returns the one-based index of
        ;;the last occurrence within the range of elements in the collection that
        ;;contains the specified number of elements and ends at the specified index.
        public method LastIndexOf, int
            required in RpsKeySegment, @RpsKeySegment
            required in StartIndex, int
            required in Count, int
        proc
            mreturn mCollection.LastIndexOf(RpsKeySegment,StartIndex,Count)
        endmethod

    endclass

endnamespace
