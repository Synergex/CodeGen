;;*****************************************************************************
;;
;; Title:       RpsFormatCollection.dbl
;;
;; Type:        Class
;;
;; Description: Represents a collection of Repository formats for a single
;;              structure, or a collection of global structures, or pre-defined
;;              date or time structures
;;
;; Date:        19th October 2007
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

.define DDINFO_DEFINES_ONLY
.include "RPSLIB:ddinfo.def"
.undefine DDINFO_DEFINES_ONLY

import System.Collections.Generic

namespace CodeGen.RepositoryAPI

    public class RpsFormatCollection extends List<RpsFormat>

        ;; Constructor (empty collection)
        public method RpsFormatCollection
            endparams
        proc
        endmethod

        ;; Constructor (structure formats)
        public method RpsFormatCollection
            required in StructureName, String
            endparams
        proc
            Repository.CheckIsOpen()
            LoadFormats(StructureName)
        endmethod

        ;; Constructor for (global or predefined formats)
        public method RpsFormatCollection
            required in GlobalFormatType, RpsGlobalFormatType
            endparams
        proc
            Repository.CheckIsOpen()
            LoadFormats(GlobalFormatType)
        endmethod

        ;;---------------------------------------------------------------------
        ;; Private methods and properties

        ;; Load structure formats
        private method LoadFormats, void
            required in StructureName, String
            endparams
            record local_data
                MemHandle, int
                ix, int
            endrecord
        proc
            init local_data
            try
            begin
                data ms_info, s_info
                ;;Set the current structure?
                dd_struct(Repository.RpsControl,DDS_INFO,StructureName,ms_info)
                if (Repository.RpsControl.error)
                    throw new RpsFormatException("Failed to get information for repository structure "+StructureName)
                ;;Do we have any formats to load?
                if (ms_info.si_nmfmts)
                begin
                    ;;Size the format names array and get the format names
                    MemHandle = %mem_proc(DM_ALLOC,^size(strIdentifier)*ms_info.si_nmfmts)
                    dd_format(Repository.RpsControl,DDM_LIST,ms_info.si_nmfmts,^m(strIdentifier,MemHandle))
                    if (Repository.RpsControl.error)
                        throw new RpsFormatException("Failed to get formats for repository structure "+StructureName)
                    ;;Add format names to the collection
                    for ix from 1 thru ms_info.si_nmfmts
                        this.Add(new RpsFormat(StructureName,^m(strIdentifier[ix],MemHandle)))
                end
            end
            finally
            begin
                ;;Free format names array
                if (MemHandle)
                    MemHandle = %mem_proc(DM_FREE,MemHandle)
            end
            endtry


        endmethod

        ;; Load global or predefined formats
        private method LoadFormats, void
            required in GlobalFormatType, RpsGlobalFormatType
            endparams
            record local_data
                MemHandle, int
                FormatCount, int
                ix, int
            endrecord
        proc
            init local_data
            try
            begin
                ;;Get the global format count
                dd_name(Repository.RpsControl,DDN_COUNT,GlobalFormatType,FormatCount)
                if (Repository.RpsControl.error)
                    throw new RpsFormatException("Failed to get repository global format count")
                ;;Do we have any formats to load?
                if (FormatCount)
                begin
                    ;;Size the format names array and get the format names
                    MemHandle = %mem_proc(DM_ALLOC,^size(strIdentifier)*FormatCount)
                    dd_name(Repository.RpsControl,DDN_LIST,GlobalFormatType,FormatCount,^m(strIdentifier,MemHandle))
                    if (Repository.RpsControl.error)
                        throw new RpsFormatException("Failed to get repository global format names")
                    ;;Add formats to collection
                    for ix from 1 thru FormatCount
                        this.Add(new RpsFormat(^m(strIdentifier[ix],MemHandle)))
                end
            end
            finally
            begin
                ;;Free format names array
                if (MemHandle)
                    MemHandle = %mem_proc(DM_FREE,MemHandle)
            end
            endtry

        endmethod

    endclass

endnamespace
