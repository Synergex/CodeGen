import System
import System.Collections.Generic
import System.Text
import HarmonyCoreGenerator.Model
import CodeGen.Engine
import System.IO

namespace HarmonyCoreGenerator.Generator

	public class ODataGenerator extends GeneratorBase

		public override method ApplyDefaults, void
			targetSolution, @HarmonyCoreGenerator.Model.Solution 
			endparams
		proc
			if(string.IsNullOrWhiteSpace(targetSolution.UnitTestsBaseNamespace) && (targetSolution.GenerateUnitTests ?? false))
			begin
				targetSolution.UnitTestsBaseNamespace = "Services.Test"
			end
		endmethod

		public override method ValidateSolution, @List<string>
			targetSolution, @HarmonyCoreGenerator.Model.Solution 
			endparams
		proc
			mreturn new List<string>()
		endmethod

		public override method GenerateTasks, @List<CodeGen.Engine.CodeGenTask>
			targetSolution, @HarmonyCoreGenerator.Model.Solution 
			endparams
		proc
			data result = new List<CodeGen.Engine.CodeGenTask>()
			;; OData controllers
			result.Add(StructureTaskHelper(targetSolution, "Generate controller classes", targetSolution.ControllersNamespace, targetSolution.ControllersFolder, false, ^nameof(ODataGenerator), new string[#] { "ODataController" }, ^null)) 

			;; EdbBuilder and Startup classes
			result.Add(StructureTaskHelper(targetSolution, "Generate the EdmBuilder and Startup classes", targetSolution.ServicesNamespace, targetSolution.ServicesFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataEdmBuilder", "ODataStartup" }, ^null)) 

			;; Self hosting program & environment
			if (targetSolution.GenerateSelfHost ?? true)
			begin
				result.Add(StructureTaskHelper(targetSolution, "Generate self hosting program and environment", targetSolution.SelfHostNamespace, targetSolution.SelfHostFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataSelfHost", "ODataSelfHostEnvironment" }, ^null)) 
			end

			;; Swagger documentation

			if ((targetSolution.VersioningOrSwagger ?? HarmonyCoreGenerator.Model.VersioningOrSwaggerMode.None) == HarmonyCoreGenerator.Model.VersioningOrSwaggerMode.GenerateSwaggerDoc)
			begin
				result.Add(StructureTaskHelper(targetSolution, "Generate swagger documentation", "", Path.Combine(targetSolution.ServicesFolder, "wwwroot"), true, ^nameof(ODataGenerator), new string[#] { "ODataSwaggerYaml" }, ^null)) 
				result.Add(StructureTaskHelper(targetSolution, "Generate swagger complex types documentation", "", Path.Combine(targetSolution.ServicesFolder, "wwwroot"), false, ^nameof(ODataGenerator), new string[#] { "ODataSwaggerType" }, ^null)) 
			end

			;; Postman tests

			if (targetSolution.GeneratePostmanTests ?? false)
			begin
                result.Add(StructureTaskHelper(targetSolution, "Generate OData Postman tests", "", targetSolution.SolutionFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataPostManTests", "PostManDevelopmentEnvironment" }, ^null)) 
			end

			;; Authentication

			if (targetSolution.CustomAuthentication ?? false)
			begin
				;; Add AuthenticationModels.dbl if it doesn't exist
				data authModelTask = StructureTaskHelper(targetSolution, "Generate custom authentication", targetSolution.ModelsNamespace, targetSolution.ModelsFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataCustomAuthModels" }, ^null)
				authModelTask.ReplaceFiles = false
				result.Add(authModelTask)
				;; Add AuthenticationController.dbl if it doesn't exist
				data authControllerTask = StructureTaskHelper(targetSolution, "Generate custom authentication", targetSolution.ControllersNamespace, targetSolution.ControllersFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataCustomAuthController" }, ^null)
				authControllerTask.ReplaceFiles = false
				result.Add(authControllerTask)
				;; Add AuthenticationTools.dbl if it doesn't exist
				data authToolsTask = StructureTaskHelper(targetSolution, "Generate custom authentication", targetSolution.ControllersNamespace, targetSolution.ControllersFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataCustomAuthTools" }, ^null)
				authToolsTask.ReplaceFiles = false
				result.Add(authToolsTask)
			end

			;;---------------------------------------------------------------------------------------------
			;; Unit tests

			if (targetSolution.GenerateUnitTests ?? false)
			begin
				;; Client-side model, data loader and unit tests
				result.Add(StructureTaskHelper(targetSolution, "Generate client-side models, data loaders and unit tests", targetSolution.UnitTestsBaseNamespace, targetSolution.UnitTestFolder, false, ^nameof(ODataGenerator), new string[#] { "ODataClientModel", "ODataTestDataLoader", "ODataUnitTests" }, ^null)) 
				
				;; Test environment
				result.Add(StructureTaskHelper(targetSolution, "Generate test environment", targetSolution.UnitTestsBaseNamespace, targetSolution.UnitTestFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataUnitTestHost" }, ^null)) 
				
				;; Unit test environment and hosting program
				result.Add(StructureTaskHelper(targetSolution, "Generate unit test environment and hosting program", targetSolution.UnitTestsBaseNamespace, targetSolution.UnitTestFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataUnitTestEnvironment", "ODataUnitTestHost" }, ^null)) 

				;; Test constants properties
				result.Add(StructureTaskHelper(targetSolution, "Generate test constants class", targetSolution.UnitTestsBaseNamespace, targetSolution.UnitTestFolder, true, ^nameof(ODataGenerator), new string[#] { "ODataTestConstantsProperties" }, ^null)) 
                result.Add(StructureTaskHelper(targetSolution, "Generate test constants values", targetSolution.UnitTestsBaseNamespace, targetSolution.UnitTestFolder + ".GenerateValues", true, ^nameof(ODataGenerator), new string[#] { "GenerateTestValues" }, ^null))
			end
			mreturn result
		endmethod
	
	endclass

endnamespace
