
import System
.ifdef DBLNET
import System.Collections.ObjectModel
import System.ComponentModel
import System.IO
import System.Xml
import System.Xml.Serialization
.endc

import CodeGen.Engine

namespace CodeGen.Engine

.ifndef DBLNET
	
	public class CodeGenTaskSet
		
		public method CodeGenTaskSet
			endparams
		proc
			throw new Exception("The CodeGenTaskSet class is only available in Synergy .NET!")
		endmethod
		
	endclass
	
.else
	
	public class CodeGenTaskSet implements INotifyPropertyChanged

		private mDescription,			String,								"New Task Set"

		private mRepositoryMainFile,	String,								String.Empty
		private mRepositoryTextFile,	String,								String.Empty

		private mTemplateFolder,		String,								String.Empty
		private mOutputFolder,			String,								String.Empty

		private mLoggingLevel,			LoggingLevel,						LoggingLevel.Normal				;; -v -d, etc.
		private mEchoCommands,			Boolean,							false							;; -e
		private mListGeneratedFiles,	Boolean,							false							;;-lf

		private mTasks,					@ObservableCollection<CodeGenTask>

		{NonSerialized}
		private mComplete,				Boolean,							false

		public method CodeGenTaskSet
			endparams
		proc
			Tasks = new ObservableCollection<CodeGenTask>()
		endmethod

		{DefaultValue("New Task Set")}
		public property Description, String
			method get
			proc
				mreturn mDescription
			endmethod
			method set
			proc
				mDescription = value
				NotifyPropertyChanged("Description")
			endmethod
		endproperty

		{DefaultValue("")}
		public property RepositoryMainFile, String
			method get
			proc
				mreturn mRepositoryMainFile
			endmethod
			method set
			proc
				mRepositoryMainFile = value
				NotifyPropertyChanged("RepositoryMainFile")
			endmethod
		endproperty
		
		{DefaultValue("")}
		public property RepositoryTextFile, String
			method get
			proc
				mreturn mRepositoryTextFile
			endmethod
			method set
			proc
				mRepositoryTextFile = value
				NotifyPropertyChanged("RepositoryTextFile")
			endmethod
		endproperty

		{DefaultValue("")}
		public property TemplateFolder, String
			method get
			proc
				mreturn mTemplateFolder
			endmethod
			method set
			proc
				mTemplateFolder = value.Trim()
				if (!mTemplateFolder.EndsWith(":")&&!mTemplateFolder.EndsWith("\"))
					mTemplateFolder = mTemplateFolder + "\"
				NotifyPropertyChanged("TemplateFolder")
			endmethod
		endproperty

		{DefaultValue("")}
		public property OutputFolder, String
			method get
			proc
				mreturn mOutputFolder
			endmethod
			method set
			proc
				mOutputFolder = value.Trim()
				if (!mOutputFolder.EndsWith(":")&&!mOutputFolder.EndsWith("\"))
					mOutputFolder = mOutputFolder + "\"
				NotifyPropertyChanged("OutputFolder")
			endmethod
		endproperty
		
		{DefaultValue(0)}
		public property LoggingLevel, LoggingLevel
			method get
			proc
				mreturn mLoggingLevel
			endmethod
			method set
			proc
				mLoggingLevel = value
				NotifyPropertyChanged("LoggingLevel")
			endmethod
		endproperty
		
		;;; <summary>
		;;;  Echo the command line to the output log
		;;; </summary>
		{DefaultValue(false)}
		public property EchoCommands, Boolean
			method get
			proc
				mreturn mEchoCommands
			endmethod
			method set
			proc
				mEchoCommands = value
				NotifyPropertyChanged("EchoCommands")
			endmethod
		endproperty
		
		{DefaultValue(false)}
		public property ListGeneratedFiles, Boolean
			method get
			proc
				mreturn mListGeneratedFiles
			endmethod
			method set
			proc
				mListGeneratedFiles = value
				NotifyPropertyChanged("ListGeneratedFiles")
			endmethod
		endproperty
		
		public property Tasks, @ObservableCollection<CodeGenTask>
			method get
			proc
				mreturn mTasks
			endmethod
			method set
			proc
				mTasks = value
			endmethod
		endproperty

		{XmlIgnore}
		public property Complete, Boolean
			method get
			proc
				mreturn mComplete
			endmethod
			internal method set
			proc
				mComplete = value
				NotifyPropertyChanged("Complete")
			endmethod
		endproperty

.region "Serialization"

		public method SaveToFile, boolean
			required in fileSpec, String
			endparams
		proc
			data ok, boolean, true
			try
			begin
				disposable data stm, @StreamWriter, File.CreateText(fileSpec)
				if(stm!=^null)
				begin
					data xs, @XmlSerializer, new XmlSerializer(this.GetType())
					xs.Serialize(stm,this)
					stm.Close()
				end
			end
			catch (e, @Exception)
			begin
				ok = false
			end
			endtry
			mreturn ok
		endmethod
		
		public static method LoadFromFile, boolean
			required in fileSpec, String
			required out taskSet, @CodeGenTaskSet
			endparams
		proc
			data ok, boolean, true
			disposable data stm, @StreamReader
			try
			begin
				stm = File.OpenText(fileSpec)
				if (stm!=^null)
				begin
					data xs, @XmlSerializer, new XmlSerializer(^typeof(CodeGenTaskSet))
					data xr, @XmlReader, new XmlTextReader(stm)
					taskSet = (CodeGenTaskSet)xs.Deserialize(xr)
					stm.Close()
				end
			end
			catch (ex, @Exception)
			begin
				ok = false
			end
			endtry
			mreturn ok
		endmethod
.endregion


.region "INotifyPropertyChanged"
		
		public event PropertyChanged, @PropertyChangedEventHandler
		
		private method NotifyPropertyChanged, void
			required in propertyName, String
			endparams
		proc
			if (PropertyChanged!=^null)
				PropertyChanged(this, new PropertyChangedEventArgs(propertyName))
		endmethod
		
.endregion
		
	endclass

.endc

endnamespace

