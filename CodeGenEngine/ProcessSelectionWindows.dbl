;;*****************************************************************************
;;
;; Title:       ProcessSelectionWindows.dbl
;;
;; Type:        Function
;;
;; Description: Locate structure fields that refer to selection windows and
;;              attempt to convert the selection windows into a selection list
;;
;; Date:        31st July 2012
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2012, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import System.Collections
import CodeGen.RepositoryAPI

namespace CodeGen.Engine
    
    function ProcessSelectionWindows,Boolean
        required inout Str          ,@RpsStructure
        required out   ErrorText    ,String
        endparams
        
        record local_data
            ok              ,Boolean                ;;Return status
            fld             ,@RpsField
        endrecord
        
    proc
        ok = true
        ErrorText = ""

        DebugLog("Attempting to resolve field selection windows",false,false)

        foreach fld in Str.Fields.GetEnumerator()
        begin
            if ((fld.SelectionListType==RpsFieldSelectionType.Window)&&(fld.SelectionWindowName))
            begin
                ;;Found a field with a selection window
                data selections, @ArrayList
                if (ParseSelectionWindow(Env.SelectionWindowScript,fld.SelectionWindowName,selections,ErrorText)) then
                begin
                    ;;We got a list of selections from a selection window, apply them to the field
                    data selItem, @a
                    fld.SelectionListType = RpsFieldSelectionType.List
                    fld.SelectionWindowName = ""
                    fld.SelectionList = new ArrayList()
                    foreach selItem in selections
                        fld.SelectionList.Add(selItem)
                end
                else
                begin
                    ok = false
                    exitloop
                end
            end
        end
        
        freturn ok
        
    endfunction
    
endnamespace

