;;*****************************************************************************
;;
;; Title:       CreateFileFromRpsFile.dbl
;;
;; Type:        Function
;;
;; Description: Creates a file from a repository file definition
;;
;; Date:        13th August 2014
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;              http://www.synergex.com
;;
;;*****************************************************************************
;;
;; Copyright (c) 2014, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************
;;
namespace CreateFile
	
	function CreateFileFromRpsFile, boolean
		required in  a_dcs          ,a			;;Repository control data
		required in  a_filename     ,String		;;Repository file definition name
		required in  a_overwrite    ,boolean	;;Overwrite file if found
		required in  a_filespec     ,String		;;Filename to create
		required out a_filecreated  ,String		;;File name created
		required out a_errtxt       ,String		;;Error text
		endparams
		
		.include "RPSLIB:ddinfo.def"
		
		stack record
			ok						,boolean
			filename				,string
		endrecord
		
	proc
		
		ok = true
		dcs = a_dcs
		a_filecreated = ""
		a_errtxt = ""
		filename = a_filename.ToUpper()
		
		;;Get the file information
		xcall dd_file(dcs,DDL_INFO,filename,fl_info)
		if (dcs.error) then
		begin
			ok = false
			a_errtxt = "Repository file " + filename + " not found!"
		end
		else
		begin
			;;Make sure the file is assigned to at least one structure
			if (!fli_nmstructs||!fli_struct)
			begin
				ok = false
				a_errtxt = "Repository file " + filename + " has no assigned structures!"
			end
		end
		
		if (ok)
			ok = DoCreateFile(dcs,filename,a_filespec,a_overwrite,a_filecreated,a_errtxt)

		freturn ok
		
	endfunction
	
endnamespace